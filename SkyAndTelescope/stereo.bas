1 REM STEREOGRAPHIC SKY PROJECTION
2 P=3.14159265: R1=P/180
3 INPUT "RADIUS ";K
4 INPUT "LAT, LONG ";B,L
5 REM W LONG NEG, E LONG POSITIVE
6 B=B*R1: L=L*R1
7 INPUT "LST (H,M,S) ";A$,A2,A3
8 GOSUB 72: T=A*15*R1-L
10 INPUT "OBJECT NAME ";B$
12 INPUT "RA (H,M,S)  ";A$,A2,A3
14 GOSUB 72: R=A*15*R1
16 INPUT "DEC (D,M,S) ";A$,A2,A3
17 GOSUB 72: D=A*R1
18 PRINT: PRINT B$
19 GOSUB 32: GOSUB 100
20 PRINT: PRINT "STARS"
21 N=8: REM  NUMBER OF STARS
22 FOR I=1 TO N
23 READ R,D
24 R=R*15*R1: D=D*R1
25 GOSUB 32: GOSUB 100
26 NEXT I
29 END
30 REM  ALTITUDE AND AZIMUTH
32 T5=T-R+L: REM LOCAL HR ANGLE
34 S1=SIN(B)*SIN(D)
36 S1=S1+COS(B)*COS(D)*COS(T5)
38 C1=1-S1*S1
40 IF C1>0 THEN C1=SQR(C1)
42 IF C1<=0 THEN 46
44 H=ATN(S1/C1): GOTO 48
46 H=SGN(S1)*P/2
48 C2=COS(B)*SIN(D)
50 C2=C2-SIN(B)*COS(D)*COS(T5)
52 S2=-COS(D)*SIN(T5)
54 IF C2=0 THEN A=SGN(S2)*P/2
56 IF C2=0 THEN 62
58 A=ATN(S2/C2)
60 IF C2<0 THEN A=A+P
62 IF A<0 THEN A=A+2*P
70 RETURN
72 REM  SEXAGESIMAL TO DECIMAL
76 S=1: A1=ABS(VAL(A$))
78 IF LEFT$(A$,1)="-" THEN S=-1
80 A=S*(A1+A2/60+A3/3600)
82 RETURN
100 REM  STEREOGRAPHIC PROJECTION
105 REM  H=ALTITUDE, A=AZIMUTH
110 Q=P/4-H/2
115 X=K*SIN(Q)/COS(Q)*SIN(2*P-A)
120 Y=K*SIN(Q)/COS(Q)*COS(2*P-A)
125 PRINT " X= ";INT(X*1000)/1000;
126 PRINT " Y= ";INT(Y*1000)/1000
130 RETURN
200 REM  STAR DATA FOR TAURUS
205 REM  RA, DEC
210 DATA 5.58, 21.0, 5.38, 28.5
215 DATA 4.55, 16.3, 4.43, 15.9
220 DATA 4.43, 19.0, 4.38, 18.0
225 DATA 4.33, 17.5, 4.29, 15.5
230 REM ************************
235 REM  FROM SKY & TELESCOPE,
240 REM  OCTOBER, 1987, P. 408
245 REM ************************
