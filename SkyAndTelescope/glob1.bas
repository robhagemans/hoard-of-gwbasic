10 REM   GLOBULAR CLUSTER PLOT
12 REM
14 R1=3.14159265/180
16 M0=+0.6:  REM  RR LYRAE STARS
18 GOSUB 66
20 PRINT
22 INPUT "NAME ";N$
24 INPUT "APPAR. MAG. (H.B.)";M1
26 INPUT "VISUAL EXTINCTION ";M2
28 D=10^(.2*(M1-M2-M0)-2)
30 D=D*3262:  REM  LIGHT-YEARS
32 INPUT "LONGITUDE";L
34 INPUT "LATITUDE ";B
36 L=L*R1:  B=B*R1
38 PRINT
40 PRINT "DISTANCE = ";D
42 IF S>2 THEN 50
44 X=D*COS(L)*COS(B)
46 PRINT " X = ";X
48 IF S<2 THEN 56
50 Y=D*SIN(L)*COS(B)
52 PRINT " Y = ";Y
54 IF S=2 THEN 60
56 Z=D*SIN(B)
58 PRINT " Z + ";Z
60 INPUT "ANOTHER (Y OR N) ";Q$
62 IF Q$<>"N" THEN 20
64 END
66 PRINT "(1) X-Z EDGE-ON VIEW"
68 PRINT "(2) X-Y FACE-ON VIEW"
70 PRINT "(3) Y-Z EDGE-ON VIEW"
72 PRINT
74 INPUT "YOUR SELECTION ";S
76 RETURN
80 REM  ------------------------
82 REM  APPEARED IN ASTRONOMICAL
84 REM  COMPUTING, SKY & TELE-
86 REM  SCOPE, DECEMBER, 1984
88 REM  ------------------------
