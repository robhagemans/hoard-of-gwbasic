100 REM    SHUTTER SPEEDS
110 REM
120 PRINT "SELECT PHOTO METHOD--"
130 PRINT "  (A) PRIME FOCUS"
140 PRINT "  (B) BARLOW PROJ."
150 PRINT "  (C) EYEPIECE PROJ."
160 PRINT "  (D) AFOCAL METHOD"
170 PRINT "  (E) QUIT"
180 INPUT A$: PRINT
190 IF A$="A" THEN 250
200 IF A$="B" THEN 250
210 IF A$="C" THEN 330
220 IF A$="D" THEN 410
230 IF A$="E" THEN END
240 GOTO 120
250 REM    CALCULATE F/NUMBER
260 INPUT "OBJECTIVE F.L.";A
270 INPUT "APERTURE";B
280 C=A/B: IF A$="A" THEN K=C
290 IF A$="A" THEN 470
300 INPUT "BARLOW POWER";D
310 K=C*D: GOTO 470
320 REM
330 REM  EYEPIECE PROJECTION
340 PRINT "SPACE FROM EYEPIECE"
350 INPUT "   TO FILM (INCHES)";A
360 INPUT "EYEPIECE F.L. (IN.)";B
370 INPUT "OBJECTIVE F.L.(IN.)";D
380 INPUT "APERTURE (IN.)";F
390 K=(A*D)/(B*F): GOTO 470
400 REM
410 REM  AFOCAL METHOD
420 INPUT "SCOPE POWER";A
430 INPUT "APERTURE (IN.)";D
440 INPUT "CAMERA F.L. (IN.)";B
450 C=A*B: K=C/D
460 REM
470 REM  CALC. SHUTTER SPEEDS
480 PRINT
490 INPUT "EXAMPLE F/NUMBER";L
500 INPUT "EXAMPLE FILM INDEX";M
510 INPUT "EXAMPLE TIME";O
520 PRINT
530 INPUT "YOUR FILM INDEX";N
540 R=(K*K)/(L*L)
550 V=R*O*M/N
560 PRINT V;" IS YOUR TIME"
570 PRINT: INPUT "NEXT";A$
580 GOTO 120
590 REM  ------------------------
600 REM  APPEARED IN ASTRONOMICAL
610 REM  COMPUTING, SKY & TELE-
620 REM  SCOPE, JUNE, 1985
630 REM  ------------------------
