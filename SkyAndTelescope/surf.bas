10 REM   SURFACE BRIGHTNESS
12 PRINT
14 INPUT "TOTAL MAGNITUDE";M$
16 IF M$="X" THEN 88   
18 M=VAL(M$): PRINT
20 PRINT "TYPE OF AREA:"
22 PRINT "  (C) CIRCULAR"
24 PRINT "  (E) ELLIPTICAL"
26 PRINT "  (R) RECTANGULAR"
28 PRINT "  (I) IRREGULAR"
30 INPUT "CHOICE";C$: PRINT
32 IF C$="C" THEN 42   
34 IF C$="E" THEN 48   
36 IF C$="R" THEN 58   
38 IF C$="I" THEN 68   
40 GOTO 30   
42 REM      CIRCULAR
44 INPUT "DIAM IN ARC MIN";D
46 A=3.14159265*D*D/4: GOTO 72   
48 REM      ELLIPTICAL
50 PRINT "ENTER ARC MIN --"
52 INPUT "  MAJOR AXIS";A1
54 INPUT "  MINOR AXIS";A2
56 A=3.14159265*A1*A2/4: GOTO 72   
58 REM      RECTANGULAR
60 PRINT "ENTER ARC MIN --"
62 INPUT "  LONG SIDE";A1
64 INPUT "  SHORT SIDE";A2
66 A=A1*A2: GOTO 72   
68 REM      IRREGULAR
70 INPUT "AREA IN SQ ARC MIN";A
72 PRINT: IF A>=1/3600 THEN 76   
74 PRINT "STARLIKE": GOTO 86   
76 S1=M+LOG(A)/LOG(100^(1/5))
78 S2=S1+8.890756
80 PRINT "SURFACE MAGNITUDE --"
82 PRINT "  PER SQ ARC MIN: ";S1
84 PRINT "  PER SQ ARC SEC: ";S2
86 GOTO 12   
88 END
90 REM ==========================
100 REM  FROM SKY & TELESCOPE'S
110 REM  ASTRONOMICAL COMPUTING
120 REM  DEPARTMENT FOR OCTOBER,
130 REM  1986, PAGE 392
140 REM =========================
