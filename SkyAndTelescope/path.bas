100 REM    TRACK A COMET
105 REM
110 GOSUB 485  
115 INPUT "NAME OF COMET";N$
120 INPUT "MONTH (3 LETTERS)";M$
125 INPUT "HOW MANY DATES (2-5)";L
130 PRINT "DAY, RA(H.M), DE(D.M):"
135 FOR I=1 TO L
140 INPUT " ";C(I,1),C(I,2),C(I,3)
145 D=C(I,2): GOSUB 430: C(I,2)=D
150 D=C(I,3): GOSUB 430: C(I,3)=D
155 NEXT I
160 INPUT "STARTING DATE";W
165 PRINT: PRINT N$
170 FOR N4=1 TO 30
175 IF W>C(L,1)+18 THEN 545  
180 PRINT: PRINT M$;W;TAB(13);
185 PRINT " RA(H.M)      DE(D.M)"
190 X=W-2
195 FOR J=1 TO 3
200 X=X+1: Y=0: Z=0
205 FOR N=1 TO L
210 S=1
215 FOR K=1 TO L
220 IF K=N THEN 230  
225 S=S*(X-C(K,1))/(C(N,1)-C(K,1))
230 NEXT K
235 Y=Y+S*C(N,2): Z=Z+S*C(N,3)
240 NEXT N
245 Q=M1+N1*SIN(Y*15*K9)*TAN(Z*K9)
250 Q=Y+Z3*Q
255 R=Z+15*Z3*N1*COS(Y*15*K9)
260 IF J<>2 THEN 305  
265 P=COS(R*K9)
270 D=Y: GOSUB 460  : Y1=D
275 D=Z: GOSUB 460  : Z1=D
280 D=Q: GOSUB 460  : Q1=D
285 D=R: GOSUB 460  : R1=D
290 PRINT Y0$;Y1;TAB(26);Z1
295 PRINT Y1$;Q1;TAB(26);R1
300 GOTO 315  
305 IF J<>1 THEN 315  
310 K1=Q: L1=R
315 NEXT J
320 Y=R-L1: REM DEGREES
325 X=(Q-K1)*P*15: REM DEGREES
330 R=SQR(X*X+Y*Y)/2: REM DEG/DAY
335 R=R*2.5
340 PRINT "  ARCSEC/MIN  ";R
345 T=ATN(X/Y)/K9
350 IF Y<0 THEN T=T+180
355 IF T<0 THEN T=T+360
360 T=INT(T+0.5)
365 PRINT "  POS ANGLE   ";T
370 Z6=R*U/143.24: Z=Z6: J=0
375 Z=Z*2
380 IF Z>8000 THEN 390  
385 J=J+1: IF J<=30 THEN 375  
390 J=J-5: Z=0.001*Z
395 PRINT "  STEPS ";Z6;"HZ"
400 PRINT "  RATIO ";J
405 PRINT "  FREQ  ";Z;" KHZ"
410 W=W+1
415 NEXT N4
420 GOTO 545  
425 REM
430 REM   H.M --> HOURS
435 S1=SGN(D): D=ABS(D)
440 D1=INT(D): D2=D-D1
445 D=S1*(D1+D2/0.6)
450 RETURN
455 REM
460 REM   HOURS --> H.M
465 S1=SGN(D): D=ABS(D)
470 D1=INT(D): D2=D-D1
475 D=S1*(D1+D2*0.6)
480 RETURN
485 REM      CONSTANTS
490 DIM C(5,3)
495 U=178.27: REM  FOCAL LENGTH
500 Y0=1950: REM  INPUT EQUINOX
505 Y1=1987: REM  CURRENT EQUINOX
510 Z3=Y1-Y0
515 Y0$="  ("+STR$(Y0)+".0)  "
520 Y1$="  ("+STR$(Y1)+".0)  "
525 M1=0.0008538: N1=0.0003711
530 P1=3.1415926536: K9=P1/180
535 RETURN
536 REM ************************
537 REM  FROM "SKY & TELESCOPE"
538 REM  ASTRONOMICAL COMPUTING
539 REM  FEB. 1987, PAGE 196
540 REM ************************
545 END
