1000 'WhizCall  01-20-05
1010 GOTO 1230 'begin
1020    SAVE "WhizCall",A:LIST-1220
1030 ON I GOTO 1020,2300,2550 'make REM for QB compiles
1040 GOTO 1880 'Qkey keyboard driver
1050 GOTO 2000 'GetM Mickey input
1060 GOTO 2200 'Mset Mickey reset
1070 GOTO 2270  'Mick interface
1080 GOTO 2360  'Time & day & date
1090 GOTO 2660 'Save options/make file
1100 GOTO 2880 'Dial numb selector
1110 GOTO 3050  'Ring numb (and OPEN)
1120 GOTO 3150 'List record lines
1130 GOTO 3320 'Pick line display
1140 GOTO 3390 'Disp LS big chars in SD
1150 GOTO 3470 'Menu edit/opts/help/quit
1160 GOTO 3680 'Help shell to WhizCall.COM
1170 GOTO 3740 'Opts port/mouse/AM;PM/File
1180 GOTO 4100 'Edit records add/chg/del
1190 GOTO 4510  'Line name/dial/ext/note
1200 GOTO 4620   'Type one field into QB
1210 GOTO 4900 'Page layout/refresh
1220 GOTO 5060 'Trap for ON ERROR
1230 'begin
1240  DEFSTR M-W:DEFINT C-L,X-Z:KEY OFF
1250  FOR I=1 TO 10:KEY I,"":NEXT:RANDOMIZE TIMER
1260  ON ERROR GOTO 1220 'Trap
1270  I=0:J=0:L=0:B=0:G=0:X=0:Y=0     'locals
1280  LX=20:LY=10                     'LocX:LocY (Time & Disp)
1290  HT=0:IT=0:LT=0                  'HadTime:IdxTime:LocTime
1300  FP=10:LP=0:FR=0                 'FilePtr:LastPtr:FileRing
1310  K1=0:K2=0:LS=0                  'Key1:Key2:LenStr
1320  GQ=0:LQ=0:CQ=0:IQ=1:KQ=0:EQ=0   'GetQ:LocQ:ColQ:InsQ:KeyQ:EndQ
1330  GX=70:GY=200:GM=0               'GetMick pos & on/off
1340  DM=1:EM=1:IM=1:GS=20            'DoMode:EdMode:IndexMenu:GetSpd
1350  DIM G(28) 'Mickey interface
1360   G(0)=&H8EB:G(5)=&H8957:G(6)=&H8BE7:G(7)=&HFE7D:G(8)=&H458B
1370   G(9)=&H8B02:G(10)=&H45D:G(11)=&H4D8B:G(12)=&H8B06:G(13)=&H855
1380   G(14)=&H559C:G(15)=&H1E56:G(16)=&HCD06:G(17)=&H733:G(18)=&H5E1F
1390   G(19)=&H9D5D:G(20)=&H5D89:G(21)=&H8904:G(22)=&H64D:G(23)=&H5589
1400   G(24)=&H5F08:G(25)=&HCB
1410  DIM C(5)                        'Call code - see Time
1420   C(0)=&H2AB4 'B42A    MOV AH,2A ; get date
1430   C(1)=&H21CD 'CD21    INT 21 ; DOS
1440   C(2)=&HC289 '89C2    MOV DX,AX ; weekday # in AL (0-6)
1450   C(3)=&H80B8 'B88025  MOV AX,2580 ; set INT 80
1460   C(4)=&HCD25 'CD21    INT 21 ; DOS
1470   C(5)=&HCB21 'CB      RETF ; answer is in 0000:0200
1480  DIM F(3072)                     'Font masks 16x24 char size
1490  DIM I(1100),H(1100)             'Images:Hold tanks
1500  RD=SPACE$(30)                   'RingData
1510  MH="2":PI="2":MF="N":PD="U"     'ModeHrs:PortIndex:MakeFile:PgDir
1520  SH=TIME$:SD=DATE$               'SysHour:SysDate
1530  TH=SPACE$(8):TD=SPACE$(10)      'TempHour:TempDate
1540  SC=" 0---------1---------2---------3---------4---------5---------"
1550  SB=STRING$(64,0)                'StrBufr
1560  Q2=MKI$(0):QB=SPACE$(80)        'Q2keys:QBufr
1570  P1="A B C D E F G H I J K L M N O P Q R S T U V W X Y Z" 'Pick1
1580  P2="              0 1 2 3 4 5 6 7 8 9 * #              " 'Pick2
1590  OPEN "WhizCall.DAT" AS 1 LEN=64:FIELD 1,64 AS RB 'RecBufr
1600   FIELD 1,2 AS R0,14 AS R1,14 AS R2,4 AS R3,27 AS R4,1 AS R5
1610   FP=10:LP=LOF(1)\64                              'last phys rcd
1620   WHILE LP=0:GOSUB 1090 'save
1630    RSET RB=MKI$(2573):PUT 1,2:FP=2:LP=2           'new DAT file
1640   WEND:GET 1,1:LSET PI=R0:FP=VAL(R1):GS=VAL(R5)
1650    LSET MH=R2:LSET MF=R3:LSET PD=R4:GET 1,LP      'get opts
1660   WHILE ASC(R1)=255 AND LP>2:GET 1,LP-1:LP=LP-1:WEND 'last logic rec
1670  GOSUB 1210:GOSUB 1060 'Page:Mset
1680  WHILE DM-4 '      *************** mainline loop
1690   GOSUB 1040:IF K1>90 THEN K1=K1-32 'Qkey
1700   I=INSTR(P2,CHR$(K1)):IF I THEN LSET RD=Q2:GOSUB 1110 'Ring
1710   IF K1=27 THEN GOSUB 1150 'Menu
1720   I=INSTR("P H Q I",CHR$(K2) )                   'dn/up/PgD/PgU
1730    IF I THEN IF PD="U" THEN I=INSTR("H P I Q",CHR$(K2))
1740    IF I THEN FP=FP+VAL(MID$("-1+1-8+8",I,2))
1750    IF FP<2 THEN FP=2 ELSE IF FP>LP THEN FP=LP
1760   I=INSTR("ABCDEFGHIJKLMNOPQRSTUVWXYZ",CHR$(K1)) 'find page
1770   WHILE I
1780    FOR I=1 TO LP:GET 1,I:C=ASC(R1)
1790     IF C>64 THEN IF C=>K1 THEN FP=I:I=LP+1
1800    NEXT:I=0:K2=1
1810   WEND
1820   IF K1=13 THEN GOSUB 1100:K2=1 'Dial
1830   IF K2 THEN GOSUB 1120 'List
1840  WEND '            *************** end mainline
1850  GOSUB 1090:G(1)=0:GOSUB 1070 'Save:Mick Reset
1860  CLOSE:RESET:SCREEN 0
1870 SYSTEM
1880 'Qkey
1890  LSET Q2=MKI$(0):IF GM THEN G(1)=1:GOSUB 1070 'Mick On
1900  WHILE CVI(Q2)=0:GOSUB 1080 'Time
1910   MID$(Q2,1)=INKEY$:IF CVI(Q2)=0 THEN GOSUB 1050 'GetM
1920  WEND:K1=ASC(Q2):K2=ASC(MID$(Q2,2))
1930  WHILE K1=27 AND FR>0:K1=0
1940   PRINT #FR,"ATH":CLOSE FR:FR=0:GOSUB 1130 'Pick
1950    LINE(578,0)-STEP(43,28),0,BF 'cls green box
1960  WEND:IF GM THEN G(1)=2:GOSUB 1070 'Mick Off
1970  IF ASC(Q2)>127 THEN 1880
1980  IF K1 AND K1<32 THEN IF NOT (K1=13 OR K1=27 OR K1=8) THEN 1880
1990 RETURN
2000 'GetM
2010  IF EQ THEN 2190
2020  G(1)=3:GOSUB 1070:GX=G(3):GY=G(4) 'Mick Stat
2030  IF G(2)*GS THEN K1=G(2) ELSE 2190 'off or no click
2040  WHILE G(2):G(1)=3:GOSUB 1070:WEND 'Mick Wait
2050  GX=G(3):GY=G(4) 'pos on release
2060  IF K1=2 THEN MID$(Q2,1)=CHR$(27):GOTO 2190 'r button
2070   IF GM=1 THEN G(1)=2:GOSUB 1070:GM=9 'Mick Off
2080   CQ=GX\8+1:LQ=GY\14+1:I=POINT(70,94) 'opts box?
2090   CQ=CQ+(CQ MOD 2):IF LQ<6 THEN CQ=4 'menu vs. line 25
2100   G(2)=1:KQ=SCREEN(LQ,CQ):I=0  'get char
2110   IF POINT(70,94)=6 THEN MID$(Q2,1)=CHR$(8):GOTO 2180  'opts box
2120   IF LQ>4 AND LQ<13 AND POINT(19,48)>0 THEN I=-(13-LQ) 'top half
2130   IF LQ>15 AND LQ<24 AND POINT(19,48)>0 THEN I=LQ-15   'bot half
2140    IF I THEN FP=FP+I:GOSUB 1120:GOTO 2180 'List
2150   IF LQ<6 AND GX<66 THEN MID$(Q2,1)=CHR$(KQ)    'menu
2160   IF LQ=25 THEN MID$(Q2,1)=CHR$(KQ)             'Pick line
2170   IF GY>174 AND GY<204 THEN MID$(Q2,1)=CHR$(13) 'dial line
2180 IF GM=9 THEN G(1)=1:GOSUB 1070:GM=1 'Mick On
2190 RETURN
2200 'Mset
2210  G(1)=0:GOSUB 1070 'Mick RST
2220  G(1)=26:G(2)=GS*20:G(3)=GS*20:G(4)=GS*20:GOSUB 1070 'Mick Speed
2230  G(1)=1:GOSUB 1070 'Mick ON
2240  G(1)=4:G(3)=GX:G(4)=GY:GOSUB 1070 'Mick POS
2250  G(1)=2:GOSUB 1070 'Mick OFF
2260 RETURN
2270 'Mick
2280  IF G(1)<3 THEN GM=G(1)
2290  IF GS THEN G=VARPTR(G(0)) ELSE 2350
2300  CALL G:GOTO 2320                   'GW
2310  CALL ABSOLUTE(G)                   'QB
2320 IF G(1)-3 THEN 2350 'status chk?
2330  IF G(3)-320 THEN 2350 ELSE IF G(4)-175 THEN 2350 'center?
2340  GOSUB 1060:G(1)=3:GOTO 2270 'Mset reinit
2350 RETURN
2360 'Time
2370  LSET SH=TIME$:IT=VAL(MH)-2:LT=GM
2380  IF GY>41 THEN LT=0 ELSE IF GX<62 OR GX>563 THEN LT=0
2390  WHILE IT '                                        AM/PM
2400   IT=12*(SH>"12:59:59")-12*(VAL(SH)=0)+VAL(SH)
2410   MID$(SH,1)=HEX$(IT\10):MID$(SH,2)=HEX$(IT MOD 10):IT=0
2420  WEND:IT=VAL(MID$(SH,7))
2430  WHILE IT-HT:HT=CSRLIN*100+POS(0) '                seconds
2440   IF LT=1 THEN G(1)=2:GOSUB 1070:LT=9 'Mick Off
2450   LOCATE 3,10:COLOR 7:PRINT SC:LOCATE HT\100,HT MOD 100
2460   HT=IT*8+79:LINE(HT,29)-STEP(8,10),15,B:HT=IT
2470  WEND:LX=78:LY=3
2480  WHILE LEFT$(SH,5)<>LEFT$(TH,5):LSET TH=SH '       time
2490   IF LT=1 THEN G(1)=2:GOSUB 1070:LT=9 'Mick Off
2500   LSET SB=LEFT$(SH,5):LS=5:GOSUB 1140 'Disp hh:mm
2510  WEND:LSET SD=DATE$
2520  WHILE SD<>TD:LSET TD=SD '                         date
2530   IF LT=1 THEN G(1)=2:GOSUB 1070:LT=9 'Mick Off
2540   IT=VARPTR(C(0))
2550   CALL IT:GOTO 2570        'GW
2560   CALL ABSOLUTE(IT)        'QB
2570 DEF SEG=0:IT=PEEK(&H200)+1:DEF SEG '               day# (1-7)
2580   LSET SB=MID$("SunMonTueWedThuFriSat",IT*3-2,3)
2590    LX=LX+16:LS=3:GOSUB 1140:IT=VAL(SD) 'Disp
2600    LSET SB=MID$("JanFebMarAprMayJunJulAugSepOctNovDec",IT*3-2,3)
2610    LX=400:LS=4:GOSUB 1140 'Disp
2620    LSET SB=MID$(SD,4,2):MID$(SB,3)=",":MID$(SB,5)=MID$(SD,9,2)
2630     LS=6:GOSUB 1140 'Disp date & yr
2640  WEND:IF LT=9 THEN G(1)=1:GOSUB 1070 'Mick On
2650 RETURN
2660 'Save
2670  RSET RB=MKI$(2573):LSET R5=HEX$(GS) 'GetSpd
2680  LSET R0=PI:LSET R1=STR$(FP) 'PortIndex:FilePtr
2690  LSET R2=MH:LSET R3=MF:LSET R4=PD:PUT 1,1 'TimeMode:MakeFile:PgDir
2700  IF DM-4 THEN 2870 'not Quit mode
2710  WHILE MF="T":LSET MF=""                      'MakeFile=TXT
2720   OPEN "WhizCall.TXT" FOR OUTPUT AS 2
2730   FOR I=2 TO LP:GET 1,I:PRINT #2,R1;R2;R3;R4:NEXT
2740  WEND
2750  WHILE MF="C":LSET MF=""                      'MakeFile=CSV
2760   OPEN "WhizCall.CSV" FOR OUTPUT AS 2
2770   FOR I=2 TO LP:GET 1,I:C=1
2780    LSET SB=R1:GOSUB 2820:LSET SB=R2:GOSUB 2820:LSET SB=R3:GOSUB 2820
2790    C=0:LSET SB=R4:GOSUB 2820
2800   NEXT
2810  WEND:GOTO 2870
2820 L=LEN(SB)
2830  WHILE L>1 AND MID$(SB,L,1)=" ":L=L-1:WEND
2840  IF L=1 THEN L=L+(ASC(SB)=32)
2850  IF C THEN PRINT #2,CHR$(34);LEFT$(SB,L);CHR$(34);",";:GOTO 2870
2860    PRINT #2,CHR$(34);LEFT$(SB,L);CHR$(34)
2870 RETURN
2880 'Dial
2890  GET 1,FP
2900  LINE(5,177)-STEP(224,24),0,BF     'clear big name
2910  COLOR 7:LOCATE 14,2:PRINT R1;     'small name
2920  LSET SB=R2:LY=178:LX=110:LS=14+1
2930  FOR I=LS TO 1 STEP-1              'adjust disp pos
2940   IF ASC(MID$(SB,I))=32 THEN LS=LS-1:LX=LX+16 ELSE I=0
2950  NEXT:GOSUB 1140 'Disp
2960  GOSUB 1040:IF K1=27 THEN 3040 'Qkey
2970  LSET RD=R2:GOSUB 1110 'Ring
2980  LINE(122,177)-STEP(230,24),0,BF   'clear big dial
2990  IF ASC(R3)=32 THEN 3040           'no ext
3000  COLOR 7:LOCATE 14,30:PRINT R2;
3010  LSET SB=R3:LY=178:LX=348:LS=4:GOSUB 1140 'Disp
3020  GOSUB 1040:IF K1=27 THEN 3040 'Qkey
3030  LSET RD=R3:GOSUB 1110 'Ring
3040 RETURN
3050 'Ring
3060  IF PI="0" OR ASC(RD)<35 THEN 3140
3070  L=CSRLIN:C=POS(0):LOCATE 1,74:COLOR 10:PRINT "Port"
3080  LOCATE 2,74:PRINT "Open";:LOCATE L,C:COLOR 7
3090   LINE(578,0)-STEP(43,28),10,B
3100  WHILE FR=0:FR=2:GOSUB 1130 'Pick
3110   LSET QB="COM":MID$(QB,4)=PI       'open MODEM
3120   OPEN LEFT$(QB,4) FOR OUTPUT AS FR
3130  WEND:PRINT #FR,"ATD";RD;";"        'dial
3140 RETURN
3150 'Page
3160  COLOR 7:IF FP<2 THEN FP=2 ELSE IF FP>LP THEN FP=LP
3170  J=FP-8:FOR L=5 TO 12:GOSUB 3270:NEXT
3180  IF POINT(0,174)=0 THEN LINE(0,174)-STEP(639,0),7 'line
3190  LINE(5,177)-STEP(635,24),0,BF
3200  LX=5:LY=178:GET 1,FP
3210   LSET SB=R1:LS=14:GOSUB 1140 'Disp name
3220   L=14:LOCATE L,30:PRINT R2;  'dial
3230   LOCATE L,46:PRINT R3;:LOCATE L,53:PRINT R4; 'ext:note
3240  IF POINT(0,205)=0 THEN LINE(0,204)-STEP(639,0),7 'line
3250  J=FP+1:FOR L=16 TO 23:GOSUB 3270:NEXT
3260  GOTO 3310
3270 IF J<2 OR J>LP THEN LSET RB="" ELSE GET 1,J  'disp rec
3280  LOCATE L,2:PRINT R1;:LOCATE L,30:PRINT R2;  'name:dial
3290  LOCATE L,46:PRINT R3;:LOCATE L,53:PRINT R4; 'ext:note
3300  J=J+1
3310 RETURN
3320 'Pick
3330  L=CSRLIN:C=POS(0):LOCATE 25,14
3340  IF FR THEN COLOR 10 ELSE COLOR 14
3350  IF GS=0 THEN PRINT SPC(52);
3360  IF GS THEN IF FR THEN PRINT P2; ELSE PRINT P1;
3370  LOCATE L,C
3380 RETURN
3390 'Disp
3400  FOR I=1 TO LS:C=ASC(MID$(SB,I)):C=C*24:Y=LY
3410   FOR J=0 TO 23
3420    LINE(LX,Y)-STEP(15,0),0
3430    LINE(LX,Y)-STEP(15,0),15,,F(J+C):Y=Y+1
3440   NEXT:LX=LX+16
3450  NEXT
3460 RETURN
3470 'Menu
3480  GET(13,9)-STEP(54,66),H:COLOR 15 'save
3490  LINE(13,9)-STEP(54,66),11,BF     'cls menu
3500  LINE(16,11)-STEP(48,62),0,BF:L=2 'box
3510  FOR I=1 TO 15 STEP 4:LOCATE L,4:L=L+1
3520   PRINT MID$("EditOptsHelpQuit",I,4);
3530  NEXT
3540  WHILE I:Y=DM*14
3550   IF GS=0 THEN GET(20,Y)-STEP(40,14),F:PUT(20,Y),F,PRESET 'btn on
3560   GOSUB 1040:I=(K2=72)-(K2=80) 'Qkey
3570   IF GS=0 THEN PUT(20,DM*14),F,PSET                       'btn off
3580   IF I THEN DM=DM+I:IF DM<1 THEN DM=4 ELSE IF DM>4 THEN DM=1
3590  WEND:I=INSTR("EOHQ",CHR$(K1))+INSTR("eohq",CHR$(K1))
3600  IF GS*(G(2)=1) THEN IF LQ>5 THEN K1=0:I=0
3610  IF K1=13 THEN K1=DM ELSE IF I THEN K1=I ELSE K1=0
3620  PUT(13,9),H,PSET:IF K1 THEN DM=K1 'restore
3630  IF K1=3 THEN GM=0:GOSUB 1160:K1=0:GM=2 'Help
3640  IF K1=2 THEN GOSUB 1170:K1=0 'Opts
3650  IF K1=1 THEN GOSUB 1180:K1=0:GM=2 'Edit
3660  GOSUB 1060 'Mset for Win+QB20
3670 RETURN
3680 'Help
3690  GOSUB 1060 'Mset Off
3700  SCREEN 0 'text mode
3710  SHELL "WhizCall.COM" 'link
3720  GOSUB 1210 'Page refresh screen
3730 RETURN
3740 'Opts
3750  PCOPY 0,1
3760  LINE(67,91)-STEP(502,196),0,B 'shadow
3770  LINE(68,92)-STEP(500,194),6,BF:LINE(76,98)-STEP(484,182),0,BF 'box
3780 GOSUB 1130:COLOR 15 'Pick
3790  LOCATE 9,15:PRINT "Port#  ";PI;
3800   PRINT,"0 will disable dialing capability";
3810  LOCATE 11,15:PRINT "Hours  ";MH;
3820   PRINT,"1 or 2 for 12 or 24 hour display";
3830  LOCATE 13,15:PRINT "Mouse ";GS;
3840   PRINT,"speed factor of 0 will disable mouse";
3850  LOCATE 15,15:PRINT "File2  ";MF;
3860   PRINT,"N/T/C make None or TXT or CSV file";
3870  LOCATE 17,15:PRINT "Page";CHR$(23);"  ";PD;
3880   PRINT,"reverse action of Up/Down & Page keys";
3890  FOR Y=111 TO 223 STEP 28:LINE(164,Y)-STEP(15,15),10,B:NEXT 'boxes
3900  LOCATE 20,12:PRINT SPACE$(59);:LOCATE 20,27:COLOR 6
3910  IF GS THEN PRINT "Click on green box to change";:GOTO 3940
3920   LOCATE 20,12:PRINT "<Enter> to next item    ";
3930   PRINT "<space> to change    <Esc> to exit"
3940 LQ=IM*28+80:COLOR 7
3950  IF GS=0 THEN GET(107,LQ)-STEP(77,21),F:PUT(107,LQ),F,PRESET
3960   GOSUB 1040:I=0:IF K1=27 THEN 4080 'Qkey
3970  IF GS=0 THEN PUT(107,LQ),F,PSET
3980   IF K1=13 THEN IM=INSTR("51234",HEX$(IM)):GOTO 3940 'next
3990   IF GS THEN I=INSTR("EGIKM",CHR$(LQ+60)) 'line 9,11,15,17,19
4000   IF I*(K1=8)*(CQ=22) THEN IM=I:K1=32 'col 22?
4010   IF K1-32 THEN BEEP:GOTO 3940 'oops
4020   ON IM GOTO 4030,4040,4050,4060,4070
4030 C=INSTR("012",PI):LSET PI=MID$("120",C):GOTO 3780          'port
4040 C=INSTR("12",MH):LSET MH=MID$("21",C):LSET TH="":GOTO 3780 'hours
4050 GS=INSTR("01234 ",HEX$(GS)):GOSUB 1060:GOTO 3780           'speed
4060 C=INSTR("NTC",MF):LSET MF=MID$("TCN",C):GOTO 3780          'file
4070 C=INSTR("DU",PD):LSET PD=MID$("UD",C):GOTO 3780            'up/down
4080 PCOPY 1,0:GOSUB 1130:GOSUB 1090 'Pick:Save
4090 RETURN
4100 'Edit
4110  GET(13,23)-STEP(46,52),H:COLOR 15
4120  LINE(13,23)-STEP(46,52),11,BF    'cls menu
4130  LINE(16,25)-STEP(40,48),0,BF:L=3 'box
4140  FOR I=1 TO 10 STEP 3:LOCATE L,4:L=L+1
4150   PRINT MID$("AddChgDel",I,3);
4160  NEXT
4170  WHILE I:Y=EM*14+14
4180   IF GS=0 THEN GET(20,Y)-STEP(32,14),I:PUT(20,Y),I,PRESET 'btn on
4190   GOSUB 1040:I=(K2=72)-(K2=80) 'Qkey
4200   IF GS=0 THEN PUT(20,EM*14+14),I,PSET                    'btn off
4210   IF I THEN EM=EM+I:IF EM<1 THEN EM=3 ELSE IF EM>3 THEN EM=1
4220  WEND:I=INSTR("ACD",CHR$(K1))+INSTR("acd",CHR$(K1))
4230  IF K1=13 THEN K1=EM ELSE IF I THEN K1=I ELSE K1=0
4240  I=EM:IF ASC(Q2)=27 THEN PUT(13,23),H,PSET:GOTO 4500
4250  IF K1 THEN EM=K1:I=EM
4260  GET(20,I*14+14)-STEP(32,14),F 'get mode
4270  PUT(13,23),H,PSET             'clear menu
4280  LINE(17,13)-STEP(38,18),2,BF  'box
4290  PUT(20,15),F,PRESET           'put mode
4300  G(1)=2:GOSUB 1070 'Mick Off
4310  WHILE I=1:LP=LP+1                          'Add
4320   FOR I=LP TO FP STEP-1:GET 1,I:PUT 1:NEXT
4330   RSET RB=MKI$(2573):PUT 1,FP 'CR/LF
4340   GOSUB 1120:GOSUB 1190:I=0 'List:Line
4350  WEND
4360  WHILE I=2:GOSUB 1190:I=0:WEND 'Line        Chg
4370  WHILE I=3:LINE(200,181)-STEP(216,16),0,BF 'Del
4380   LOCATE 14,27:COLOR 11:GQ=INT(RND*10)
4390   PRINT "To confirm delete hit <";HEX$(GQ);">";
4400    GET(200,181)-STEP(216,16),I:PUT(200,181),I,PRESET
4410    GOSUB 1040:PUT(200,181),I,PSET 'Qkey
4420    IF K1-48-GQ THEN BEEP:I=0 ELSE I=99
4430    WHILE I=99
4440     FOR I=FP+1 TO LP:GET 1,I:PUT 1,I-1:NEXT 'move rest
4450     RSET RB=MKI$(2573) 'spaces+CR/LF
4460     LSET R1=CHR$(255):PUT 1,LP:LP=LP+(LP>1):I=0 'mark last
4470    WEND
4480  WEND:LINE(17,13)-STEP(39,18),0,BF:GOSUB 1120 'List
4490  G(1)=1:GOSUB 1070 'Mick On
4500 RETURN
4510 'Line
4520  LINE(5,177)-STEP(224,24),0,BF:GET 1,FP:LQ=14
4530  EQ=1:CQ=2 :GQ=14:LSET QB=R1:GOSUB 1200:LSET R1=QB 'Type name
4540  EQ=0:IF K1=27 THEN 4610
4550  EQ=1:CQ=30:GQ=14:LSET QB=R2:GOSUB 1200:LSET R2=QB 'Type dial
4560  EQ=0:IF K1=27 THEN 4610
4570  EQ=1:CQ=46:GQ=4 :LSET QB=R3:GOSUB 1200:LSET R3=QB 'Type ext
4580  EQ=0: IF K1=27 THEN 4610
4590  EQ=1:CQ=53:GQ=27:LSET QB=R4:GOSUB 1200:LSET R4=QB 'Type note
4600  EQ=0:IF K1-27 THEN PUT 1,FP 'update disk
4610 RETURN
4620 'Type
4630  KQ=CQ:EQ=CQ+GQ-1:COLOR 15
4640  COLOR 15:LOCATE LQ,CQ:PRINT LEFT$(QB,GQ);:LOCATE LQ,CQ
4650 KQ=KQ+(KQ>EQ)-(KQ<CQ):LOCATE LQ,KQ
4660  X=KQ*8-8:Y=LQ*14-14:GET(X,Y)-STEP(7,13),F:PUT(X,Y),F,PRESET
4670  GOSUB 1040:COLOR 15 'Qkey
4680  X=KQ*8-8:Y=LQ*14-14:PUT(X,Y),F,PSET
4690  IF K1=27 THEN 4880 ELSE IF K1=13 THEN 4840 ELSE IF ASC(Q2)<32 THEN 4730
4700  IF IQ=0 THEN PRINT LEFT$(Q2,1);:KQ=KQ+1:GOTO 4650                   'prt
4710   FOR I=EQ TO KQ STEP-1:LOCATE LQ,I:PRINT CHR$(SCREEN(LQ,I-1));:NEXT 'ins
4720   LOCATE LQ,KQ-(KQ<CQ):PRINT LEFT$(Q2,1);:KQ=KQ+1:GOTO 4650
4730 ON INSTR("MKSRGO",RIGHT$(Q2,1)) GOTO 4750,4760,4780,4800,4810,4820,4840
4740  IF ASC(Q2)=8 THEN 4770 ELSE 4840
4750 KQ=KQ+1:GOTO 4650                                         'M=rht
4760 KQ=KQ-1:GOTO 4650                                         'K=lft
4770 KQ=KQ+(KQ>CQ):LOCATE LQ,KQ                                '8=backsp
4780 FOR I=KQ+1 TO EQ:PRINT CHR$(SCREEN(LQ,I));:NEXT           'S=Del
4790  LOCATE LQ,EQ:PRINT " ";:GOTO 4650
4800 IQ=IQ XOR 1:GOTO 4650                                     'R=Ins
4810 KQ=CQ:GOTO 4650                                           'G=home
4820 KQ=EQ:WHILE SCREEN(LQ,KQ)<33:KQ=KQ-1:WEND:KQ=KQ+1:I=0     'O=End
4830  IF KQ<CQ THEN 4810 ELSE 4650
4840 FOR I=CQ TO CQ+GQ-1:MID$(QB,I)=CHR$(SCREEN(LQ,I)):NEXT    'exit
4850  WHILE INSTR(QB,CHR$(0)):MID$(QB,INSTR(QB,CHR$(0)))=" ":WEND
4860  LSET QB=MID$(QB,CQ,GQ):IF LEFT$(QB,GQ)=SPACE$(GQ) THEN 4880
4870  WHILE ASC(QB)=32:LSET QB=MID$(QB,2,GQ):WEND 'left justify
4880 COLOR 7:LOCATE LQ,CQ:PRINT LEFT$(QB,GQ);
4890 RETURN
4900 'Page
4910  SCREEN 9:COLOR 15:CLS
4920  LOCATE 2,32:COLOR 6             'prog/version
4930   PRINT "WhizCall Ver 1.0"
4940   GET(242,14)-STEP(138,12),F:LINE(242,14)-STEP(138,12),0,BF
4950   LINE(241,8)-STEP(139,12),7,BF:PUT(241,8),F,XOR
4960  LOCATE 3,10:COLOR 7:PRINT SC    'seconds
4970  COLOR 6                         'ribbon line
4980   LOCATE 4,2:PRINT "Name";:LOCATE 4,29:PRINT "³Dial";
4990   LOCATE 4,45:PRINT "³Ext";:LOCATE 4,52:PRINT "³Note";
5000   GET(5,42)-STEP(630,13),F:PUT(5,42),F,PRESET:COLOR 15
5010   BLOAD "WhizCall.FNT",VARPTR(F(0)) 'orig from WhizFont.WF2
5020   HT=0:IT=0:LT=0 'HadTime:IdxTime:LocTime
5030  LSET TH="":LSET TD=""
5040   GOSUB 1080:GOSUB 1120:GOSUB 1130 'Time:List:Pick
5050 RETURN
5060 'Trap
5070  BEEP:LOCATE 25,1
5080  PRINT,"Error";ERR;"in line";ERL,"Hit any key to resume.";
5090  LSET Q2=MKI$(0):WHILE CVI(Q2)=0:MID$(Q2,1)=INKEY$:WEND
5100  RESUME 5110
5110 SYSTEM
5120 'convert WhizFont.WF2 to BLOAD file ************ Dev & Debug
5130 DEFSTR M-Z:DEFINT A-L:CLS:COLOR 15,0:Q=MKI$(0)
5140  DIM B(3072) '6144/2 (ea char=48 bytes; 128*48=6144 bytes)
5150  OPEN "whizfont.wf2" AS 1 LEN=2:FIELD 1,2 AS R
5160  FOR I=0 TO 3071:GET 1:B(I)=CVI(R):NEXT:CLOSE:DEF SEG=&HA000
5170  FOR I=0 TO 3071
5180   LINE(0,0)-STEP(15,0),15,,B(I)
5190   LSET Q=CHR$(PEEK(0)):MID$(Q,2)=CHR$(PEEK(1)):B(I)=CVI(Q)
5200   LINE(0,0)-STEP(15,0),0
5210  NEXT:DEF SEG:BSAVE "whizcall.fnt",VARPTR(B(0)),6144
